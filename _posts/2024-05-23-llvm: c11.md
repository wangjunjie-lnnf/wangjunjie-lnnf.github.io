---
layout: post
title:  "llvm: c11"
date:   2024-05-23 21:49:07 +0000
categories: llvm
tags: llvm
---

# c11

书接上文，目前已经到达了解析源文件生成`AST`的过程，此处要暂停源码的分析，改为总结`c11`的语法规则。之前尝试过很多次分析`clang`和`gcc`的实现机制，每次都卡在这里，后来总结了一下，虽然还算熟悉`c`语言，但是很多细节和规则并不了解，导致分析源码时很快就陷入晕头转向的境地。

本文主要内容来自`ISO/IEC 9899:2011`，对一些很偏僻的点做了简化和省略。

## Conceptual models

### Program structure

> A C program need not all be translated at the same time. The text of the program is kept in units called source files, (or preprocessing files). A source file together with all the headers and source files included via the preprocessing directive `#include` is known as a preprocessing translation unit. After preprocessing, a preprocessing translation unit is called a translation unit. Previously translated translation units may be preserved individually or in libraries. The separate translation units of a program communicate by (for example) calls to functions whose identifiers have external linkage, manipulation of objects whose identifiers have external linkage, or manipulation of data files. Translation units may be separately translated and then later linked to produce an executable program.

C程序的源文件不要求同时编译，每个源文件及其通过`#include`递归包含的所有源文件和头文件合在一起称作一个预处理单元，预处理之后称作一个转换单元。一个程序的多个转换单元之间可以通过调用外部可见的函数或访问外部可见的数据结构来互操作。多个转换单元通过链接产生可执行程序。

### Translation phases

每个源文件的处理过程如下：

1. Physical source file multibyte characters are mapped, in an implementation-defined manner, to the source character set (introducing new-line characters for end-of-line indicators) if necessary. Trigraph sequences are replaced by corresponding single-character internal representations.

> 字符集转换

2. Each instance of a backslash character (\) immediately followed by a new-line character is deleted, splicing physical source lines to form logical source lines. Only the last backslash on any physical source line shall be eligible for being part of such a splice. A source file that is not empty shall end in a new-line character, which shall not be immediately preceded by a backslash character before any such splicing takes place.

> 物理行转逻辑行: 删除每行末尾的`\`及紧随其后的换行符

3. The source file is decomposed into preprocessing tokens and sequences of white-space characters (including comments). A source file shall not end in a partial preprocessing token or in a partial comment. Each comment is replaced by one space character. New-line characters are retained. Whether each nonempty sequence of white-space characters other than new-line is retained or replaced by one space character is implementation-defined.

> 注释替换为空格，然后以`连续的空格`为分隔符拆分成`preprocessing tokens`

4. Preprocessing directives are executed, macro invocations are expanded, and `_Pragma` unary operator expressions are executed. If a character sequence that matches the syntax of a universal character name is produced by token concatenation, the behavior is undefined. A `#include` preprocessing directive causes the named header or source file to be processed from phase 1 through phase 4, recursively. All preprocessing directives are then deleted.

> 执行预处理指令，遇到`#include`时，对被包含的文件递归执行步骤1到4的处理

5. Each source character set member and escape sequence in character constants and string literals is converted to the corresponding member of the execution character set; if there is no corresponding member, it is converted to an implementation-defined member other than the null (wide) character.

> 对字符常量和字符串常量做字符集转换

6. Adjacent string literal tokens are concatenated.

> 连接相邻的字符串常量

7. White-space characters separating tokens are no longer significant. Each preprocessing token is converted into a token. The resulting tokens are syntactically and semantically analyzed and translated as a translation unit.

> 每个源文件预处理之后成为一个`translation unit`

8. All external object and function references are resolved. Library components are linked to satisfy external references to functions and objects not defined in the current translation. All such translator output is collected into a program image which contains information needed for execution in its execution environment.

> 解析全局的数据对象和函数声明


## Lexical elements

> A token is the minimal lexical element of the language in translation phases 7 and 8. The categories of tokens are: keywords, identifiers, constants, string literals, and punctuators. A preprocessing token is the minimal lexical element of the language in translation phases 3 through 6. The categories of preprocessing tokens are: header names, identifiers, preprocessing numbers, character constants, string literals, punctuators, and single non-white-space characters that do not lexically match the other preprocessing token categories. Preprocessing tokens can be separated by white space; this consists of comments (described later), or white-space characters (space, horizontal tab, new-line, vertical tab, and form-feed), or both.

token是最小的词法元素，包含关键字、标识符、常量、字符串、标点；预处理token可以被空白和注释分割。

```bnf

# 编译阶段识别的token
token:
    keyword
    identifier
    constant
    string-literal
    punctuator

# 预处理阶段识别的token
preprocessing-token:
    header-name
    identifier
    pp-number
    character-constant
    string-literal
    punctuator
    each non-white-space character that cannot be one of the above

```

### Keywords

```c

keyword: one of
    `unsigned`  `signed`    `void`  `const` `static`
    `auto`      `register`  `restrict`      `volatile`
    `char`      `short`     `int`   `long`  `float`  `double`
    `do`        `while`
    `for`       `break`     `continue`
    `if`        `else`
    `switch`    `case`      `default`  `goto`  `return`
    `enum`      `struct`    `union`
    `extern`    `inline`
    `sizeof`    `typedef`   
    `_Alignas`  `_Alignof`  `_Atomic`   `_Bool`
    `_Complex`  `_Generic`  `_Imaginary`    `_Noreturn`
    `_Static_assert`    `_Thread_local`

```

### Identifiers

```bnf

# 非数字开头的(_|a-z|A-Z|0-9)的组合
identifier:
    identifier-nondigit
    identifier identifier-nondigit
    identifier digit

identifier-nondigit:
    nondigit
    universal-character-name
    other implementation-defined characters

nondigit: one of
    _ a b c d e f g h i j k l m 
      n o p q r s t u v w x y z 
      A B C D E F G H I J K L M 
      N O P Q R S T U V W X Y Z 

digit: one of
    0 1 2 3 4 5 6 7 8 9

universal-character-name:
    \u hex-quad
    \U hex-quad hex-quad

hex-quad:
    hexadecimal-digit hexadecimal-digit hexadecimal-digit hexadecimal-digit

```

### Constants

```bnf

constant:
    integer-constant
    floating-constant
    enumeration-constant
    character-constant

# An identifier declared as an enumeration constant has type int
enumeration-constant:
    identifier

# 单个字符
character-constant:
     ' c-char-sequence '
    L' c-char-sequence '
    u' c-char-sequence '
    U' c-char-sequence '

c-char-sequence:
    c-char
    c-char-sequence c-char

c-char:
    any member of the source character set except the single-quote ', backslash \, or new-line character
    escape-sequence

escape-sequence:
    simple-escape-sequence
    octal-escape-sequence
    hexadecimal-escape-sequence
    universal-character-name

simple-escape-sequence: one of
    \'  \"  \?  \\
    \a  \b  \f  \n  \r  \t  \v

```

### String literals

```bnf

string-literal:
    encoding-prefix[opt] " s-char-sequence[opt] "

encoding-prefix:
    `u8`
    `u`
    `U`
    `L`

s-char-sequence:
    s-char
    s-char-sequence s-char

s-char:
    any member of the source character set except the double-quote(`"`), backslash(`\`), or new-line character
    escape-sequence

```

### Punctuators

> A punctuator is a symbol that has independent syntactic  and semantic significance. Depending on context, it may specify an operation to be performed (which in turn may yield a value or a function designator, produce a side effect, or some combination thereof) in which case it is known as an operator (other forms of operator also exist in some contexts). An operand is an entity on which an operator acts.

```bnf

punctuator: one of
    [ ] ( ) { } . -> 
    ++ -- & * + - ~ !
    / % << >> < > <= >= == != ^ | && || 
    ? : ; ... 
    = *= /= %= += -= <<= >>= &= ^= |=
    , # ## 
    <: :> <% %> %: %:%:

```

`<: :> <% %> %: %:%:`这6个符号的含义同`[ ] { } # ##`

### Header names

```bnf

header-name:
    < h-char-sequence >
    " q-char-sequence "

h-char-sequence:
    h-char
    h-char-sequence h-char

h-char:
    any member of the source character set except the new-line character and >

q-char-sequence:
    q-char
    q-char-sequence q-char
q-char:
    any member of the source character set except the new-line character and "

```

## Expressions

> An expression is a sequence of operators and operands that specifies computation of a value, or that designates an object or a function, or that generates side effects, or that performs a combination thereof. The value computations of the operands of an operator are sequenced before the value computation of the result of the operator.

表达式是一组操作符及其操作数用于计算出一个值

### Primary expressions

```bnf

primary-expression:
    identifier
    constant
    string-literal
    ( expression )
    generic-selection
        `_Generic` ( assignment-expression , generic-assoc-list )

```

### Postfix operators

```bnf

postfix-expression:
    primary-expression
    postfix-expression [ expression ]
    postfix-expression ( argument-expression-list[opt] )
    postfix-expression . identifier
    postfix-expression -> identifier
    postfix-expression ++
    postfix-expression --
    ( type-name ) { initializer-list }
    ( type-name ) { initializer-list , }

argument-expression-list:
    assignment-expression
    argument-expression-list , assignment-expression

```

#### Array subscripting

> A postfix expression followed by an expression in square brackets [] is a subscripted designation of an element of an array object. The definition of the subscript operator [] is that `E1[E2]` is identical to `(*((E1)+(E2)))`. Because of the conversion rules that apply to the binary + operator, if E1 is an array object (equivalently, a pointer to the initial element of an array object) and E2 is an integer, `E1[E2]` designates the E2-th element of E1 (counting from zero).

访问数组元素转换为计算地址

> Successive subscript operators designate an element of a multidimensional array object. If E is an n-dimensional array (n ≥ 2) with dimensions `i × j × . . . × k`, then E (used as other than an lvalue) is converted to a pointer to an (n − 1)-dimensional array with dimensions `j × . . . × k`. If the unary * operator is applied to this pointer explicitly, or implicitly as a result of subscripting, the result is the referenced (n − 1)-dimensional array, which itself is converted into a pointer if used as other than an lvalue. It follows from this that arrays are stored in row-major order (last subscript varies fastest).

多维数组: `int[3][5]`表示`(int[5])[3]`而不是`(int[3])[5]`

#### Function calls

> A postfix expression followed by parentheses () containing a possibly empty, comma-separated list of expressions is a function call. The postfix expression denotes the called function. The list of expressions specifies the arguments to the function.

#### Compound literals

> A postfix expression that consists of a parenthesized type name followed by a brace-enclosed list of initializers is a compound literal. It provides an unnamed object whose value is given by the initializer list.

```c

int *p = (int []){2, 4};

void drawline(struct point* point1, struct point* point2);
drawline(&(struct point){.x=1, .y=1}, &(struct point){.x=3, .y=4});

```

### Unary operators

一元操作符

```bnf

unary-expression:
    postfix-expression
    ++ unary-expression
    -- unary-expression
    unary-operator cast-expression
    `sizeof` unary-expression
    `sizeof` ( type-name )
    `_Alignof` ( type-name )

unary-operator: one of
    & * + - ~ !

```

### Cast operators

```bnf

cast-expression:
    unary-expression
    ( type-name ) cast-expression

```

### Multiplicative operators

```bnf

multiplicative-expression:
    cast-expression
    multiplicative-expression * cast-expression
    multiplicative-expression / cast-expression
    multiplicative-expression % cast-expression

```

### Additive operators

```bnf

additive-expression:
    multiplicative-expression
    additive-expression + multiplicative-expression
    additive-expression - multiplicative-expression

```

### Bitwise shift operators

```bnf

shift-expression:
    additive-expression
    shift-expression << additive-expression
    shift-expression >> additive-expression

```

### Relational operators

```bnf

relational-expression:
    shift-expression
    relational-expression < shift-expression
    relational-expression > shift-expression
    relational-expression <= shift-expression
    relational-expression >= shift-expression

equality-expression:
    relational-expression
    equality-expression == relational-expression
    equality-expression != relational-expression

```

### Bitwise operators

```bnf

AND-expression:
    equality-expression
    AND-expression & equality-expression

exclusive-OR-expression:
    AND-expression
    exclusive-OR-expression ^ AND-expression

inclusive-OR-expression:
    exclusive-OR-expression
    inclusive-OR-expression | exclusive-OR-expression

```

### Logical operators

```bnf

logical-AND-expression:
    inclusive-OR-expression
    logical-AND-expression && inclusive-OR-expression

logical-OR-expression:
    logical-AND-expression
    logical-OR-expression || logical-AND-expression

```

### Conditional operator

```bnf

conditional-expression:
    logical-OR-expression
    logical-OR-expression ? expression : conditional-expression

```

### Assignment operators

```bnf

assignment-expression:
    conditional-expression
    unary-expression assignment-operator assignment-expression
    assignment-operator: one of
        = *= /= %= += -= <<= >>= &= ^= |=

```

### Comma operator

```bnf

expression:
    assignment-expression
    expression , assignment-expression

```

> The left operand of a comma operator is evaluated as a void expression; there is a sequence point between its evaluation and that of the right operand. Then the right operand is evaluated; the result has its type and value.

逗号表达式的左边的值被忽略，右边的值作为表达式的最终结果

```c

f(a, (t=3, t+2), c);

```

## Constant expressions

> A constant expression can be evaluated during translation rather than runtime, and accordingly may be used in any place that a constant may be.

常量表达式的值在编译时而不是运行时计算

```bnf

constant-expression:
    conditional-expression

```

## Declarations

```bnf

declaration:
    declaration-specifiers init-declarator-list[opt] ;
    static_assert-declaration
        `_Static_assert` ( constant-expression , string-literal ) ;

# 5种类型任意顺序混合
declaration-specifiers:
    storage-class-specifier declaration-specifiers[opt]
    type-specifier declaration-specifiers[opt]
    type-qualifier declaration-specifiers[opt]
    function-specifier declaration-specifiers[opt]
    alignment-specifier declaration-specifiers[opt]

init-declarator-list:
    init-declarator
    init-declarator-list , init-declarator

init-declarator:
    declarator
    declarator = initializer

```

> If an identifier has no linkage, there shall be no more than one declaration of the identifier
(in a declarator or type specifier) with the same scope and in the same name space, except that: a `typedef` name may be redefined to denote the same type as it currently does,
provided that type is not a variably modified type.

一个标识符在同一个命名空间只能声明一次，除了`typedef`声明的类型


### Storage-class specifiers

```bnf

storage-class-specifier:
    `typedef`
    `extern`
    `static`
    `_Thread_local`
    `auto`
    `register`

```

### Type specifiers

```bnf

type-specifier:
    `void`
    `char`
    `short`
    `int`
    `long`
    `float`
    `double`
    `signed`
    `unsigned`
    `_Bool`
    `_Complex`
    atomic-type-specifier
    struct-or-union-specifier
    enum-specifier
    typedef-name

```

```bnf

struct-or-union-specifier:
    struct-or-union identifier[opt] { struct-declaration-list }
    struct-or-union identifier

struct-or-union:
    `struct`
    `union`

struct-declaration-list:
    struct-declaration
    struct-declaration-list struct-declaration

struct-declaration:
    specifier-qualifier-list struct-declarator-list[opt] ;
    static_assert-declaration

specifier-qualifier-list:
    type-specifier specifier-qualifier-list[opt]
    type-qualifier specifier-qualifier-list[opt]

struct-declarator-list:
    struct-declarator
    struct-declarator-list , struct-declarator

struct-declarator:
    declarator
    declarator[opt] : constant-expression

```

```bnf

enum-specifier:
    enum identifier[opt] { enumerator-list }
    enum identifier[opt] { enumerator-list , } 
    enum identifier

enumerator-list:
    enumerator
    enumerator-list , enumerator

enumerator:
    enumeration-constant
    enumeration-constant = constant-expression

```

```bnf

atomic-type-specifier:
    `_Atomic` ( type-name )

```

### Type qualifiers

```bnf

type-qualifier:
    `const`
    `restrict`
    `volatile`
    `_Atomic`

```

### Function specifiers

```bnf

function-specifier:
    `inline`
    `_Noreturn`

```

### Alignment specifiers

```bnf

alignment-specifier:
    `_Alignas` ( type-name )
    `_Alignas` ( constant-expression )

```

### Declarators

```bnf

declarator:
    pointer[opt] direct-declarator

direct-declarator:
    identifier
    ( declarator )
    direct-declarator [ type-qualifier-list[opt] assignment-expression[opt] ]
    direct-declarator [ `static` type-qualifier-list[opt] assignment-expression ]
    direct-declarator [ type-qualifier-list `static` assignment-expression ]
    direct-declarator [ type-qualifier-list[opt] * ] 
    direct-declarator ( parameter-type-list )
    direct-declarator ( identifier-list[opt] )

pointer:
    * type-qualifier-list[opt]
    * type-qualifier-list[opt] pointer

type-qualifier-list:
    type-qualifier
    type-qualifier-list type-qualifier

parameter-type-list:
    parameter-list
    parameter-list , ...

parameter-list:
    parameter-declaration
    parameter-list , parameter-declaration

parameter-declaration:
    declaration-specifiers declarator
    declaration-specifiers abstract-declarator[opt]

identifier-list:
    identifier
    identifier-list , identifier

```

### Type names

```bnf

type-name:
    specifier-qualifier-list abstract-declarator[opt]

abstract-declarator:
    pointer
    pointer[opt] direct-abstract-declarator

direct-abstract-declarator:
    ( abstract-declarator )
    direct-abstract-declarator[opt] [ type-qualifier-list[opt]
    assignment-expression[opt] ]
    direct-abstract-declarator[opt] [ `static` type-qualifier-list[opt]
    assignment-expression ]
    direct-abstract-declarator[opt] [ type-qualifier-list `static`
    assignment-expression ]
    direct-abstract-declarator[opt] [ * ] 
    direct-abstract-declarator[opt] ( parameter-type-list[opt] )

```

### Type definitions

```bnf

typedef-name:
    identifier

```

### Initialization

```bnf

initializer:
    assignment-expression
    { initializer-list }
    { initializer-list , } 

initializer-list:
    designation[opt] initializer
    initializer-list , designation[opt] initializer

designation:
    designator-list =

designator-list:
    designator
    designator-list designator

designator:
    [ constant-expression ]
    . identifier

```

### Static assertions

```bnf

static_assert-declaration:
    `_Static_assert` ( constant-expression , string-literal ) ;

```

## Statements and blocks

> A statement specifies an action to be performed. Except as indicated, statements are executed in sequence.

```bnf

statement:
    labeled-statement
    compound-statement
    expression-statement
    selection-statement
    iteration-statement
    jump-statement

```

### Labeled statements

```bnf

labeled-statement:
    identifier : statement
    `case` constant-expression : statement
    `default` : statement

```

### Compound statement

```bnf


compound-statement:
    { block-item-list[opt] }

block-item-list:
    block-item
    block-item-list block-item

block-item:
    declaration
    statement

```

### Expression and null statements

```bnf

expression-statement:
    expression[opt] ;

```

### Selection statements

```bnf

selection-statement:
    `if` ( expression ) statement
    `if` ( expression ) statement `else` statement
    `switch` ( expression ) statement

```

### Iteration statements

```bnf

iteration-statement:
    `while` ( expression ) statement
    `do` statement `while` ( expression ) ; 
    `for` ( expression[opt] ; expression[opt] ; expression[opt] ) statement
    `for` ( declaration expression[opt] ; expression[opt] ) statement

```

### Jump statements

```bnf

jump-statement:
    `goto` identifier ;
    `continue` ;
    `break` ;
    `return` expression[opt] ;

```

## External definitions

> The unit of program text after preprocessing is a translation unit, which consists of a sequence of external declarations. These are described as ‘external’ because they appear outside any function (and hence have file scope). A declaration that also causes storage to be reserved for an object or a function named by the identifier is a definition.

预处理之后的转换单元由一组外部声明组成，所谓外部声明是说这些声明在函数之外，是文件级别的。需要保留存储空间的声明属于定义（例如：`int a = 1;`）。

```bnf

translation-unit:
    external-declaration
    translation-unit external-declaration

external-declaration:
    function-definition
    declaration

```

### Function definitions

```bnf

function-definition:
    declaration-specifiers declarator declaration-list[opt] compound-statement

declaration-list:
    declaration
    declaration-list declaration

```

### External object definitions

> If the declaration of an identifier for an object has file scope and an initializer, the declaration is an external definition for the identifier.

声明时指定初始值表示定义

> A declaration of an identifier for an object that has file scope without an initializer, and
without a storage-class specifier or with the storage-class specifier `static`, constitutes a
`tentative` definition. If a translation unit contains one or more tentative definitions for an
identifier, and the translation unit contains no external definition for that identifier, then
the behavior is exactly as if the translation unit contains a file scope declaration of that
identifier, with the composite type as of the end of the translation unit, with an initializer equal to 0.

声明时未指定初始值且未指定`static/extern`等存储修饰符表示临时定义，临时定义可以出现多次。如果一个转换单元内某个标识符只有临时定义，则转换为初始值为0的定义。


## Preprocessing directives

> A preprocessing directive consists of a sequence of preprocessing tokens that satisfies the following constraints: The first token in the sequence is a `#` preprocessing token that (at the start of translation phase 4) is either the first character in the source file (optionally after white space containing no new-line characters) or that follows white space containing at least one new-line character. The last token in the sequence is the first new-line character that follows the first token in the sequence.

```bnf

preprocessing-file:
    group[opt]

group:
    group-part
    group group-part

group-part:
    if-section
    control-line
    text-line
    # non-directive

if-section:
    if-group elif-groups[opt] else-group[opt] endif-line

if-group:
    # `if` constant-expression new-line group[opt]
    # `ifdef` identifier new-line group[opt]
    # `ifndef` identifier new-line group[opt]

elif-groups:
    elif-group
    elif-groups elif-group

elif-group:
    # `elif` constant-expression new-line group[opt]

else-group:
    # `else` new-line group[opt]

endif-line:
    # `endif` new-line

control-line:
    # `include` pp-tokens new-line
    # `define`  identifier replacement-list new-line
    # `define`  identifier ( identifier-list[opt] ) replacement-list new-line
    # `define`  identifier ( ... ) replacement-list new-line
    # `define`  identifier ( identifier-list , ... ) replacement-list new-line
    # `undef`   identifier new-line
    # `line`    pp-tokens new-line
    # `error`   pp-tokensopt new-line
    # `pragma`  pp-tokensopt new-line
    #           new-line

text-line:
    pp-tokens[opt] new-line

non-directive:
    pp-tokens new-line

replacement-list:
    pp-tokens[opt]

pp-tokens:
    preprocessing-token
    pp-tokens preprocessing-token

new-line:
    `the new-line character`

```

### Macro replacement

> After the arguments for the invocation of a function-like macro have been identified, argument substitution takes place. A parameter in the replacement list, unless preceded by a `#` or `##` preprocessing token or followed by a `##` preprocessing token, is replaced by the corresponding argument after all macros contained therein have been expanded. Before being substituted, each argument’s preprocessing tokens are completely macro replaced as if they formed the rest of the preprocessing file; no other preprocessing tokens are available.

body中参数出现且没有前置`#/##`或后置`##`时，首先对参数值做扩展，然后做替换

> If, in the replacement list, a parameter is immediately preceded by a `#` preprocessing token, both are replaced by a single character string literal preprocessing token that contains the spelling of the preprocessing token sequence for the corresponding argument. 

`#`表示在body中把紧随其后的参数的值不做扩展直接转成字符串

> If, in the replacement list of a function-like macro, a parameter is immediately preceded or followed by a `##` preprocessing token, the parameter is replaced by the corresponding argument’s preprocessing token sequence.

`##`表示在body中把前后紧邻的参数的值保持原样

```c

// 非函数macro的body中的#号只是普通字符，##表示连接两边的字符串
#define hash_hash # ## #    // ##把其前后的两个#转为字符串
#define mkstr(a) #a
#define in_between(a) mkstr(a)
#define join(c, d) in_between(c hash_hash d)
// char p[] = "x ## y";
// 替换过程：
// 1. join(x, y)
// 2. in_between(x hash_hash y)
// 3. in_between(x ## y)    // 扩展之后的结果递归扩展
// 4. mkstr(x ## y)         // 扩展之后产生的##不会生效
// 5. "x ## y"
char p[] = join(x, y); 

```

> After all parameters in the replacement list have been substituted and `#` and `##` processing has taken place, all placemarker preprocessing tokens are removed. The resulting preprocessing token sequence is then rescanned, along with all subsequent preprocessing tokens of the source file, for more macro names to replace. 

body中的参数替换和字符串化结束之后的字符串，跟后续的源文件一起被重新扫描进行预处理

> If the name of the macro being replaced is found during this scan of the replacement list (not including the rest of the source file’s preprocessing tokens), it is not replaced. Furthermore, if any nested replacements encounter the name of the macro being replaced, it is not replaced. These nonreplaced macro name preprocessing tokens are no longer available for further replacement even if they are later (re)examined in contexts in which that macro name preprocessing token would otherwise have been replaced.

当前正在被扩展的宏名称在其本次整个递归扩展过程中无法被再次扩展: 宏扩展在llvm中的实现类似函数调用的栈帧，当宏第一次出现在扩展栈帧时标记其禁止扩展，直到递归扩展完全结束从扩展栈帧弹出时才会再次标记其可扩展

> The resulting completely macro-replaced preprocessing token sequence is not processed as a preprocessing directive even if it resembles one.

扩展结束之后的字符串当作源文件处理，处理之前标记禁止预处理，处理完之后再恢复预处理，继续处理后续的源文件

```c

#define AA 11 
#define BB 22 

#define STR(x) #x
#define STR2(x) STR(x)
#define CONNECT(a, b) a##_##b
#define CONNECT2(a, b) CONNECT(a, b)

// "name"
char *p10 = STR(name);
// "#name" 
char *p11 = STR(#name);

// body中带#时其后的参数值不管是啥都作为单个字符串

// "CONNECT(11, 22)"
char *p20 = STR(CONNECT(11, 22));
// "CONNECT(AA, BB)"
char *p21 = STR(CONNECT(AA, BB));
// "CONNECT2(AA, BB)"
char *p22 = STR(CONNECT2(AA, BB));

// body中的参数前没有#时先扩展再替换
// ##前后的token保持原样

// "AA_BB"
char *p30 = STR2(CONNECT(AA, BB));

// "11_22"
// 1. CONNECT2(AA, BB) = CONNECT(11, 22) = 11_22
// 2. STR2(CONNECT2(AA, BB)) = STR(11_22) = "11_22"
char *p40 = STR2(CONNECT2(AA, BB));

```