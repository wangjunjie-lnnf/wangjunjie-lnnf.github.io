---
layout: post
title:  "VHDL快速入门"
date:   2024-02-13 23:22:07 +0000
categories: jekyll
tags: hardware
---

# VHDL基础

> A hardware description language allows a digital system to be designed and debugged at a higher level of abstraction than schematic capture with gates,flip-flops, and standard MSI building blocks. The details of the gates and flip-flops do not need to be handled during early phases of design. A design can be entered in what is called a **behavioral description** of the design. In a behavioral HDL description, one only specfies the general working of the design at flow-chart or algorithmic level without associating to any specific physical parts, components, or implementations. Another method to enter a design in VHDL, and Verilog is the **structural description** entry. In structural design, specfic components or specific implementations of components are associated with the design. A structural VHDL or Verilog model of a design can be considered as a textual description of a schematic diagram that you would have drawn interconnecting specific gates and flip-flops.

HDL提高了抽象层次，在设计电路时暂时忽略底层的gate和flip-flop的实现，专注于描述行为和结构。

> VHDL is a hardware description language used to describe the behavior and structure of digital systems. VHDL is a general-purpose HDL that can be used to describe and simulate the operation of a wide variety of digital systems, ranging in complexity from a few gates to an interconnection of many complex integrated circuits. VHDL can describe a digital system at several different levels: **behavioral, data flow, and structural**. For example, a binary adder could be described at the behavioral level in terms of its function of adding two binary numbers without giving any implementation details. The same adder could be described at the data flow level by giving the logic equations for the adder. Finally, the adder could be described at the structural level by specifying the gates and the interconnections between the gates that comprise the adder.

VHDL是一个通用目的的硬件描述语言，支持在行为/数据流/结构化三个层次描述电路

> In general, VHDL is not case sensitive; that is, uppercase and lowercase letters are treated the same by the compiler and by the simulator. 

VHDL大小写不敏感

## 组合逻辑

> VHDL models combinational circuits by what are called **concurrent statements**. Concurrent statements are statements which are always ready to execute. These are statements, which get evaluated any time and every time a signal on the right side of the statement changes. The VHDL simulator monitors the right side of each concurrent statement, and anytime a signal changes, the expression on the right side is immediately re-evaluated. **The new value is assigned to the signal on the left side after an appropriate delay. This is exactly the way the hardware works**. Anytime a gate input changes, the gate output is recomputed by the hardware, and the output changes after the gate delay. The location of the concurrent statement in the program is not important.

类似verilog的并行赋值语句，右操作数发生变化会触发左操作数的更新，赋值语句语法如下：

`signal_name <= expression [after delay];`

> The expression is evaluated when the statement is executed, and the signal on the left side is scheduled to change after delay. The square brackets indicate that after delay is optional; they are not part of the statement. If after delay is omitted, then the signal is scheduled to be updated after a delta delay. Note that the time at which the statement executes and the time at which the signal is updated are not the same.

```VHDL
CLK <= not CLK after 10 ns;
```

上述例子自带循环效果，这与C类语言有明显的差异

## 模块

> The general structure of a VHDL module is an **entity** description and an **architecture** description. The entity description declares the input and output signals, and the architecture description specifies the internal operation of the module. 

```VHDL
entity entity-name is
    [port(list-of-interface-signals: mode type [:= initial-value]; 
          list-of-interface-signals: mode type [:= initial-value]);]
end [entity] [entity-name];
```

> Mode indicates the direction of information; whether information is flowing into the port or out of it. Input port signals are of mode **in**, output port signals are of mode **out**, and bidirectional signals are of mode **inout**. Type specifies the data type or kind of information that can be communicated. The optional initial-value is used to initialize the signals on the associated list; otherwise, the default initial value is used for the specified type. 

entity用于声明模块的外部接口，包括信号的名称、模式、类型、默认值

```VHDL
architecture architecture-name of entity-name is
    [declarations]
begin
    architecture-body
end [architecture] [architecture-name];
```

> Associated with each entity is one or more architecture declarations. In the **declarations** section, we can declare signals and components that are used within the architecture. The architecture body contains statements that describe the operation of the module. The architecture name is arbitrary, but the entity name must match the name used in the associated entity declaration. 

一个entity可以关联多个architecture，architecture中包含信号和外部组件的声明，以及此模块具体的操作，例如：

```VHDL
entity FullAdder is
    port(X, Y, Cin: in bit; --Inputs
         Cout, Sum: out bit); --Outputs
end FullAdder;

architecture Equations of FullAdder is
begin   -- concurrent assignment statements
    Sum <= X xor Y xor Cin after 10 ns;
    Cout <= (X and Y) or (X and Cin) or (Y and Cin) after 10 ns;
end Equations;
```

> Any time a module created in one part of the code has to be used in another part, a component declaration needs to be used. The component declaration does not need to be in the same file where you are using the component. It can be where the component entity and architecture are defined. It is typical to create libraries of components for reuse in code, and typically the component declarations are placed in the library file. 

```VHDL
component component-name
    port(list-of-interface-signals-and-their-types);
end component;
```

> The port clause used in the component declaration has the same form as the port clause used in an entity declaration. The connections to each component used in a circuit are specified using a component instantiation statement of the form:

`label: component-name port map (list-of-actual-signals);`

> The list of actual signals must correspond one-to-one to the list of interface signals specified in the component declaration

当一个模块要被模块调用时，需要先声明此模块的类型，类似于C语言的头文件中声明的函数原型。使用组件时要按位置一对一的指定端口映射，类似于C语言中函数调用时指定参数。例如利用上例中的FullAdder创建一个支持4-bit加法的模块如下：

```VHDL
entity Adder4 is
    port(A, B: in bit_vector(3 downto 0); Ci: in bit; -- Inputs
         S: out bit_vector(3 downto 0); Co: out bit); -- Outputs
end Adder4;

architecture Structure of Adder4 is
    component FullAdder
        port (X, Y, Cin: in bit;    -- Inputs
              Cout, Sum: out bit);  -- Outputs
    end component;

    signal C: bit_vector(3 downto 1); -- C is an internal signal
begin --instantiate four copies of the FullAdder
    FA0: FullAdder port map (A(0), B(0), Ci, C(1), S(0));
    FA1: FullAdder port map (A(1), B(1), C(1), C(2), S(1));
    FA2: FullAdder port map (A(2), B(2), C(2), C(3), S(2));
    FA3: FullAdder port map (A(3), B(3), C(3), Co, S(3));
end Structure;
```

### buffer模式

```VHDL
entity gates is
    port(A, B, C: in bit; D, E: out bit);
end gates;

architecture example of gates is
begin
    D <= A or B after 5 ns; -- statement 1
    E <= C or D after 5 ns; -- statement 2
end example;
```

上例编译会失败，原因是D被声明为**out**模式，但是第二个语句把D当作了右操作数，如果把D改为**inout**模式又无法准备表达意图，因此引入了一个新的模式**buffer**，类似**out**，但是可以在**architecture**中作为右操作数。

```VHDL
entity gates is
    port(A, B, C: in bit; D: buffer bit; E: out bit);
end gates;

architecture example of gates is
begin
    D <= A or B after 5 ns; -- statement 1
    E <= C or D after 5 ns; -- statement 2
end example;
```

## 时序逻辑

> Modeling sequential logic requires primitives to model selective activity conditional on clock, edge-triggered devices, sequence of operations, and so on. A VHDL process has the following basic form:

```VHDL
process(sensitivity-list)
begin
    sequential-statements
end process;
```

> When a process is used, the statements between the begin and the end are executed sequentially. The expression in parentheses after the word process is called a sensitivity list, and the process executes whenever any signal in the sensitivity list changes. 

process用于表示时序逻辑，支持指定敏感列表、边缘触发、顺序执行。例如：

```VHDL
process(A, B, C, D)
begin
    C <= A and B;   -- sequential
    E <= C or D;    -- statements
end process;
```

> VHDL processes can be used for modeling combinational logic and sequential logic; however, processes are not necessary for modeling combinational logic. They are, however, required for modeling sequential logic. One should be very careful when using processes to represent combinational logic. 

processes等价于verilog的always，即可用于组合电路又可用于时序电路，用于组合电路时左操作数如果存在未更新的条件则会产生latch


### Flip-Flop

```VHDL
entity FlipFlop is
    port(CLK, ClrN: in bit;
         D: in bit;
         Q: out bit);
end FlipFlop;

architecture demo of FlipFlop is 
begin
    process(CLK, ClrN)
    begin
        if CLrN = '0' 
            then Q <= '0';
        elsif CLK'event and CLK = '1'
            then Q <= D;
        end if;
    end process;
end demo;
```

> The if statement tests for a rising edge of the clock, and Q is set equal to D when a rising edge occurs. The expression `CLK'event` is used to accomplish the functionality of an edge-triggered device. The expression `'event` is a predefined attribute for any signal. There are two types of signal attributes in VHDL, those that return values and those that return signals. The `'event` attribute returns a value. The expression `CLK'event` (read as “clock tick event”) is TRUE whenever the signal CLK changes. If `CLK = '1'` is also TRUE, this means that the change was from '0' to '1', which is a rising edge.

`CLK'event and CLK = '1'`用于检查CLK是否上升沿，if语句只能用在process中，语法如下：

```VHDL
if condition then
    sequential-statements1
else 
    sequential-statements2
end if;

if condition then
    sequential-statements
elsif condition then
    sequential-statements
else 
    sequential-statements
end if;

```

### wait

> An alternative form for a process uses wait statements instead of a sensitivity list. A process must have either a sensitivity list or wait statements. A process cannot have both wait statements and a sensitivity list. A process with wait statements may have the form: 

```VHDL
process
begin
    sequential-statements
    wait-statement
    sequential-statements
    wait-statement
    ...
end process;
```

> This process will execute the sequential-statements until a wait statement is encountered. Then it will wait until the specified wait condition is satisfied. It will then execute the next set of sequential-statements until another wait is encountered. It will continue in this manner until the end of the process is reached. Then it will start over again at the beginning of the process. Wait statements can be of three different forms:

```VHDL
wait on sensitivity-list;
wait for time-expression;
wait until Boolean-expression;
```

> The first form waits until one of the signals on the sensitivity-list changes. For example, `wait on A, B, C;` waits until A, B, or C changes and then execution proceeds. The second form waits until the time specified by time-expression has lapsed. If `wait for 5 ns;` is used, the process waits for 5 ns before continuing. If `wait for 0 ns;` is used, the wait is for one delta time. Wait statements of the form `wait for xxx ns;` are useful for writing VHDL code for simulation; however, they should not be used when writing VHDL code for synthesis since they are not synthesizable. For the third form of wait statement, the `Boolean-expression` is evaluated whenever one of the signals in the expression changes, and the process continues execution when the expression evaluates to TRUE. For example, `wait until A = B;` will wait until either A or B changes. Then `A = B` is evaluated and if the result is TRUE, the process will continue; otherwise, the process will continue to wait until A or B changes again and `A = B` is TRUE.

VHDL的process支持另外一种用法，时序操作语句和wait语句接替出现，wait支持更精细化的控制

> After a VHDL simulator is initialized, it executes each process with a sensitivity list one time through, and then waits at the beginning of the process for a change in one of the signals on the sensitivity list. If a process has a wait statement, it will initially execute until a wait statement is encountered. The following two processes are equivalent:

```VHDL
process(A, B, C, D)
begin
    C <= A and B after 5 ns;
    E <= C or D after 5 ns;
end process;

process
begin
    C <= A and B after 5 ns;
    E <= C or D after 5 ns;
    wait on A, B, C, D;
end process;
```


## 数据类型

VHDL预定义的类型包括bit(0或1)、boolean(true或false)、integer等，VHDL也支持自定义类型。VHDL是强类型语言，不同类型不能赋值。

> Predefined VHDL operators can be grouped into seven classes:
1. Binary logical operators: **and or nand nor xor xnor**
2. Relational operators: **= /= < <= > >=**
3. Shift operators: **sll srl sla sra rol ror**
4. Adding operators: + - & (concatenation)
5. Unary sign operators: + -
6. Multiplying operators: * / mod rem
7. Miscellaneous operators: not abs **

> When parentheses are not used, operators in class 7 have highest precedence and are applied first, followed by class 6, then class 5, and so on. Class 1 operators have lowest precedence and are applied last. Operators in the same class have the same precedence and are applied from left to right in an expression. The precedence order can be changed by using parentheses. 

VHDL的运算符分为7类，优先级从低到高





