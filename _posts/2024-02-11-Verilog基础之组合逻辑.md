---
layout: post
title:  "Verilog基础之组合逻辑"
date:   2024-02-11 11:22:07 +0000
categories: jekyll
tags: hardware
---

# Verilog基础

> The original Verilog standard (IEEE 1364) has been updated numerous times since its creation in 1995. The most significant update occurred in 2001, which was titled IEEE 1394–2001. In 2005 minor corrections and improvements were added to the standard, which resulted in IEEE 1394–2005. The functionality of Verilog (e.g., operators, signal types, functions, etc.) is defined within the Verilog standard, thus it is not necessary to explicitly state that a design is using the IEEE 1394 package because it is inherent in the use of Verilog.

Verilog标准是IEEE-1364，最新版本是IEEE-1394–2005，原因是2005之后Verrilog被合并入了SystemVerilog。


## 基础结构

![v-module](/assets/images/2024-02-11/v-module.png)

> A Verilog design describes a single system in a single file. The file has the suffix *.v. Within the file, the system description is contained within a module. The module includes the interface to the system (i.e., the inputs and outputs) and the description of the behavior.

module是最基础的结构，描述了输入输出端口的名称及类型。

> Verilog is case sensitive. Also, each Verilog assignment, definition, or declaration is terminated with a semicolon (;). As such, line wraps are allowed and do not signify the end of an assignment, definition, or declaration. Line wraps can be used to make Verilog more readable. Comments in Verilog are supported in two ways. The first way is called a line comment and is preceded by two slashes (i.e., //). Everything after the slashes is considered a comment until the end of the line. The second comment approach is called a block comment and begins with /* and ends with a */. Everything between /* and */ is considered a comment. A block comment can span multiple lines. All user-defined names in Verilog must start with an alphabetic letter, not a number. 

Verilog是大小写敏感的，每个赋值、定义、声明语句都以分号结尾，支持行注释和块注释。

### 数据类型

#### Value Set

| Value | Description | 
| - | - | 
| 0         | A logic zero, or false condition. | 
| 1         | A logic one, or true condition. | 
| x or X    | Unknown or uninitialized. | 
| z or Z    | High impedance, tri-stated, or floating. | 

> Verilog supports four basic values that a signal can take on: 0, 1, X, and Z. Most of the pre-defined data types in Verilog store these values.

Verilog的数据类型可以有4个值：0、1、x、z，x表示未初始化，可能是0也可能是1；z表示浮动，既不是0也不是1。

> In Verilog, these values also have an associated strength. The strengths are used to resolve the value of a signal when it is driven by multiple sources.

| Strength | Strength Level | Description | 
| - | - | - | 
|supply1 | 7 | Supply drive for V<sub>CC</sub> | 
|supply0 | 7 | Supply drive for V<sub>SS</sub>, or GND | 
|strong1 | 6 | Strong drive to logic one | 
|strong0 | 6 | Strong drive to logic zero | 
|pull1   | 5 | Medium drive to logic one | 
|pull0   | 5 | Medium drive to logic zero | 
|large   | 4 | Large capacitive | 
|weak1   | 3 | Weak drive to logic one | 
|weak0   | 3 | Weak drive to logic zero | 
|medium  | 2 | Medium capacitive | 
|small   | 1 | Small capacitive | 
|highz1  | 0 | High impedance with weak pull-up to logic one | 
|highz0  | 0 | High impedance with weak pull-down to logic zero | 

> When a signal is driven by multiple drivers, it will take on the value of the driver with the highest strength. If the two drivers have the same strength, the value will be unknown. If the strength is not specified, it will default to strong drive, or level 6.

Verilog中的值同时关联一个强度属性，当一个信号连接到多个源时，信号的值取所有源中信号最强的源的值，如果有冲突，则信号值未知，未指定强度的源默认为6。

---

#### Net Data Types

| Type | Description |
| - | - | 
wire    | A simple connection between components.
wor     | Wired-OR. If multiple drivers, their values are OR’d together.
wand    | Wired-AND’d. If multiple drivers, their values are AND’d together.
supply0 | Used to model the V<sub>SS</sub>, (GND), power supply (supply strength inherent).
supply1 | Used to model the V<sub>CC</sub> power supply (supply strength inherent).
tri     | Identical to wire. Used for readability for a net driven by multiple sources.
trior   | Identical to wor. Used for readability for nets driven by multiple sources.
triand  | Identical to wand. Used for readability for nets driven by multiple sources.
tri1    | Pulls up to logic one when tri-stated.
tri0    | Pulls down to logic zero when tri-stated.
trireg  | Holds the last value when tri-stated (capacitance strength inherent).


> Every signal within Verilog must be associated with a data type. A net data type is one that models an interconnection (aka., a net) between components and can take on the values 0, 1, X, and Z. A signal with a net data type must be driven at all times and updates its value when the driver value changes. The most common synthesizable net data type in Verilog is the wire. There are also a variety of other more advanced net data types that model complex digital systems with multiple drivers for the same net. Each of these net types can also have an associated drive strength.

> net数据类型用于表示组件之间的连接，最常用的可综合net类型是wire。

---

#### Variable Data Types

| Type | Description |
| - | - | 
reg     | A variable that models logic storage. Can take on values 0, 1, X, and Z.
integer | A 32-bit, 2’s complement variable representing whole numbers between -2,147,483,64810 to +2,147,483,647.
real    | A 64-bit, floating-point variable representing real numbers between –(2.2x10-308)10 to +(2.2x10308)10.
time    | An unsigned, 64-bit variable taking on values from 010 to +(9.2x1018).
realtime | Same as time. Just used for readability.


> Verilog also contains data types that model storage. These are called variable data types. A variable data type can take on the values 0, 1, X, and Z but does not have an associated strength. Variable data types will hold the value assigned to them until their next assignment.

variable数据类型用于表示存储，保存值不变直到下次赋值，常用的variable类型是reg。

---

#### Vectors

> In Verilog, a vector is a one-dimensional array of elements. All of the net data types, in addition to the variable type reg, can be used to form vectors. The syntax for defining a vector is as follows:  

> `<type>[<MSB_index>:<LSB_index>] vector_name` 

> While any range of indices can be used, it is common practice to have the LSB index start at zero. 

```verilog
wire[7:0] Sum; // This defines an 8-bit vector called “Sum” of type wire. The
                // MSB is given the index 7 while the LSB is given the index 0.

reg[15:0] Q; // This defines a 16-bit vector called “Q” of type reg.
```

> Individual bits within the vector can be addressed using their index. Groups of bits can be accessed using an index range. 

```verilog
Sum[0]; // This is the least significant bit of the vector “Sum” defined above.
Q[15:8]; // This is the upper 8-bits of the 16-bit vector “Q” defined above.
```

vector表示一维数组，定义时指定可索引范围，支持按范围索引。

---

#### Arrays

> An array is a multi-dimensional array of elements. This can also be thought of as a “vector of vectors.” Vectors within the array all have the same dimensions. To declare an array, the element type and dimensions are defined first followed by the array name and its dimensions. It is common practice to place the start index of the array on the left side of the “:” when defining its dimensions. The syntax for the creation of an array is shown below.

> `<element_type> [<MSB_index>:<LSB_index>] array_name [<array_start_index>:
<array_end_index>];`

```verilog
reg[7:0] Mem[0:4095];   // Defines an array of 4096, 8-bit vectors of type reg.
integer A[1:100];       // Defines an array of 100 integers.
```

> When accessing an array, the name of the array is given first, followed by the index of the element. It is also possible to access an individual bit within an array by adding and appending the index of an element.

```verilog
Mem[2]; // This is the 3rd element within the array named “Mem”.
        // This syntax represents an 8-bit vector of type reg.
Mem[2][7];  // This is the MSB of the 3rd element within the array named “Mem”.
            // This syntax represents a single bit of type reg.
A[2];   // This is the 2nd element within the array named “A”. Recall
        // that A was declared with a starting index of 1.
        // This syntax represents a 32-bit, signed integer.
```

array表示二维数组，基本元素是vector。

---

#### Numbers

> If a number is simply entered into Verilog without identifying syntax, it is treated as an integer. However, Verilog supports defining numbers in other bases. Verilog also supports an optional bit size and sign of a number. When defining the value of arrays, the “_” can be inserted between numerals to improve readability. The “_” is ignored by the Verilog compiler. Values of numbers can be entered in either upper or lower case (i.e., z or Z, f or F, etc.). The syntax for specifying the base of a number is as follows: 

> `<size_in_bits>’<base><value>`

> Note that specifying the size is optional. If it is omitted, the number will default to a 32-bit vector with leading zeros added as necessary. The supported bases are as follows:

| Syntax | Description |
| - | - |
'b  | Unsigned binary.
'o  | Unsigned octal.
'd  | Unsigned decimal.
'h  | Unsigned hexadecimal.
'sb | Signed binary.
'so | Signed octal.
'sd | Signed decimal.
'sh | Signed hexadecimal.

```verilog
10          // This is treated as decimal 10, which is a 32-bit signed vector.
4'b1111     // A 4-bit number with the value 1111.
8'b1011_0000 // An 8-bit number with the value 10110000.
8'hFF       // An 8-bit number with the value 11111111.
8'hff       // An 8-bit number with the value 11111111.
6'hA        // A 6-bit number with the value 001010. Note that leading zeros
            // were added to make the value 6-bits.
8'd7        // An 8-bit number with the value 00000111.
32'd0       // A 32-bit number with the value 0000_0000.
'b1111      // A 32-bit number with the value 0000_000F.
8'bZ        // An 8-bit number with the value ZZZZ_ZZZZ.
```

数字类型常量支持指定base、符号和bit宽度，默认bit宽度是32

---

#### Assigning

> Verilog is said to be a weakly typed (or loosely typed) language, meaning that it permits assignments between different data types. This is as opposed to a strongly typed language (such as VHDL) where signal assignments are only permitted between like types. The reason Verilog permits assignment between different types is because it treats all of its types as just groups of bits. When assigning between different types, Verilog will automatically truncate or add leading bits as necessary to make the assignment work. 

```verilog
ABC_TB = 2’b00; // ABC_TB will be assigned 3’b000. A leading bit is automatically added.
ABC_TB = 5;     // ABC_TB will be assigned 3’b101. The integer is truncated to 3-bits.
ABC_TB = 8;     // ABC_TB will be assigned 3’b000. The integer is truncated to 3-bits.
```

verilog是弱类型语言，把所有类型都当作一组bit，不同类型之间可以赋值，自动截断或前置加0


### 模块

> All systems in Verilog are encapsulated inside of a module. Modules can include instantiations of lower-level modules in order to support hierarchical designs. The keywords module and endmodule signify the beginning and end of the system description. When working on large designs, it is common practice to place each module in its own file with the same name.

```verilog
module module_name (port_list and port_definitions); // Verilog-2001 and after
    // module_items
endmodule
```

模块是基本构造，模块可以包括其他模块的实例以支持层次化设计。

---

#### Port Definitions

> The first item within a module is its definition of the inputs and outputs, or ports. Each port needs to have a user-defined name, a direction, and a type. The user-defined port names are case sensitive and must begin with an alphabetic character. The port directions are declared to be one of the three types: **input, output, and inout**. A port can take on any of the previously described data types, but **only wires, registers, and integers are synthesizable**. Port names with the same type and direction can be listed on the same line separated by commas.

```verilog
module Bus(output reg[31:0] bus_out,
           input wire[31:0] bus_in);
    // module items go here ...
endmodule
```

模块的端口要指定方向和类型，只有wire、reg、integer三种类型是可综合的。

---

#### Signal Declarations

> A signal that is used for internal connections within a system is declared within the module before its first use. Each signal must be declared by listing its type followed by a user-defined name. Signal names of like types can be declared on the same line separated with a comma. All of the legal data types described above can be used for signals; however, **only types net, reg, and integer will synthesize directly**. The syntax for a signal declaration is as follows:

> `<type> name;`

```verilog
wire node1;         // declare a signal named “node1” of type wire
reg Q2, Q1, Q0;     // declare three signals named “Q2”, “Q1”, and “Q0”, all of type reg
wire[63:0] bus1;   // declare a 64-bit vector named “bus1” with all bits of type wire
integer i,j;        // declare two integers called “i” and “j”
```

模块内部可以定义signal，相当于C语言中函数内部定义的本地变量。

> Verilog supports a hierarchical design approach, thus signal names can be the same within a subsystem as those at a higher level without conflict. 

![v-module-signal](/assets/images/2024-02-11/v-module-signal.png)

verilog有作用域的概念，每个模块都是一个作用域。

---

#### Parameter Declarations

> A parameter, or constant, is useful for representing a quantity that will be used multiple times in the architecture. The syntax for declaring a parameter is as follows:

> `parameter <type> constant_name = <value>;`

> Note that the type is optional and can only be integer, time, real, or realtime. If a type is provided, the parameter will have the same properties as a variable of the same time. If the type is excluded, the parameter will take on the type of the value assigned to it. Once declared, the constant name can be used throughout the module.

```verilog
parameter BUS_WIDTH = 32;
parameter NICKEL = 8’b0000_0101;

wire[BUS_WIDTH-1:0] BUS_A; // It is acceptable to add a “space” for readability
```

模块也可以定义常量，常量类型可以是integer, time, real, or realtime，如果未指定类型，则取值类型。

---

#### Compiler Directives

> A compiler directive provides additional information to the simulation tool on how to interpret the Verilog model. A compiler directive is placed before the module definition and is preceded with a backtick (i.e., `). Note that this is not an apostrophe. A few of the most commonly used compiler directives are as follows:

| Syntax | Description | 
| - | - | 
\`timescale `<unit>,<precision>` | Defines the timescale of the delay unit and its smallest precision.
\`include `<filename>` | Includes additional files in the compilation.
\`define `<macroname> <value>` | Declares a global constant.

编译器指令用于给仿真工具提供额外信息。


### Verilog Operators

#### Assignment Operator

> Verilog uses the equal sign (=) to denote an assignment. The left-hand side (LHS) of the assignment is the target signal. The right-hand side (RHS) contains the input arguments and can contain signals, constants, and operators.

```verilog
F1 = A;     // F1 is assigned the signal A
F2 = 8’hAA; // F2 is an 8-bit vector and is assigned the value 10101010
```

---

#### Bitwise Logical Operators

> Bitwise operators perform logic functions on individual bits. The inputs to the operation are single bits, and the output is a single bit. In the case where the inputs are vectors, each bit in the first vector is operated on by the bit in the same position from the second vector. If the vectors are not of the same length, the shorter vector is padded with leading zeros to make both lengths equal. Verilog contains the following bitwise operators:

| Syntax | Operation |
| - | - |
~   | Negation
&   | AND
\|  | OR
^   | XOR
~^ or ^~ | XNOR
<<  | Logical shift left (fill empty LSB location with zero)
\>> | Logical shift right (fill empty MSB location with zero)

bit级逻辑操作支持与或非等操作，如果操作数长度不一致，则前面补0

```verilog
~X      // invert each bit in X
X & Y   // AND each bit of X with each bit of Y
X | Y   // OR each bit of X with each bit of Y
X ^ Y   // XOR each bit of X with each bit of Y
X ~^ Y  // XNOR each bit of X with each bit of Y
X << 3  // Shift X left 3 times and fill with zeros
Y >> 2  // Shift Y right 2 times and fill with zeros
```

--- 

#### Reduction Logic Operators

> A reduction operator is one that uses each bit of a vector as individual inputs into a logic operation and produces a single-bit output. Verilog contains the following reduction logic operators.

| Syntax | Operation |
| - | - |
&       | AND all bits in the vector together (1-bit result)
~&      | NAND all bits in the vector together (1-bit result)
\|      | OR all bits in the vector together (1-bit result)
~\|     | NOR all bits in the vector together (1-bit result)
^       | XOR all bits in the vector together (1-bit result)
~^ or ^~ | XNOR all bits in the vector together (1-bit result)

reduction操作符都是一元操作符

```verilog
&X      // AND all bits in vector X together
~&X     // NAND all bits in vector X together
|X      // OR all bits in vector X together
~|X     // NOR all bits in vector X together
^X      // XOR all bits in vector X together
~^X     // XNOR all bits in vector X together
```

---

#### Boolean Logic Operators

> A Boolean logic operator is one that returns a value of TRUE (1) or FALSE (0) based on a logic operation of the input operations. These operations are used in decision statements.

| Syntax | Operation |
| - | - |
!   | Negation
&&  | AND
\|\|  | OR

boolean操作符用于if语句等

---

#### Relational Operators

> A relational operator is one that returns a value of TRUE (1) or FALSE (0) based on a comparison of two inputs.

| Syntax | Operation |
| - | - |
==  | Equality
!=  | Inequality
<   | Less than
\>  | Greater than
<=  | Less than or equal
\>=  | Greater than or equal

---

#### Conditional Operators

> Verilog contains a conditional operator that can be used to provide a more intuitive approach to modeling logic statements. 

> `<target_net> = <Boolean_condition> ? <true_assignment> : <false_assignment>;`

```verilog
// If A is a zero, F=1, otherwise F=0. This models an inverter.
F = (A == 1'b0) ? 1'b1 : 1'b0; 

// If sel is a zero, F=A, otherwise F=B. This models a selectable switch.
F = (sel == 1'b0) ? A : B; 

// Nested conditional statements. This models an XOR gate.
F = ((A == 1'b0) && (B == 1'b0)) ? 1'b'0 : 
    ((A == 1'b0) && (B == 1'b1)) ? 1'b'1 : 
    ((A == 1'b1) && (B == 1'b0)) ? 1'b'1 :
    ((A == 1'b1) && (B == 1'b1)) ? 1'b'0;

// This models the logic expression F = C'*(A'+B).
F = ( !C && (!A || B) ) ? 1'b1 : 1'b0; 
```

---

#### Concatenation Operator

> In Verilog, the curly brackets (i.e., {}) are used to concatenate multiple signals. The target of this operation must be the same size as the sum of the sizes of the input arguments.

```verilog
// Assuming Bus1, Bus2, and Bus3 are all 8-bit vectors, 
// this operation takes the upper 4-bits of Bus2, 
// concatenates them with the lower 4-bits of Bus3, 
// and assigns the 8-bit combination to Bus1.
Bus1[7:0] = {Bus2[7:4], Bus3[3:0]}; 

// If BusA and BusB are 4-bits, then BusC must be 8-bits.
BusC = {BusA, BusB};

// This pads the 4-bit vector BusA with 4x leading zeros and assigns to the 8-bit vector BusC.
BusC[7:0] = {4’b0000, BusA}; 
```

连接操作符用于连接多个signal

---

#### Replication Operator

> Verilog provides the ability to concatenate a vector with itself through the replication operator. This operator uses double curly brackets (i.e., {{}}) and an integer indicating the number of replications to be performed. The replication syntax is as follows:

> `{<number_of_replications>{<vector_name_to_be_replicated>}}`

```verilog
BusX = {4{Bus1}};   // This is equivalent to: BusX = {Bus1, Bus1, Bus1, Bus1};
BusY = {2{A,B}};    // This is equivalent to: BusY = {A, B, A, B};
BusZ = {Bus1, {2{Bus2}}};   // This is equivalent to: BusZ = {Bus1, Bus2, Bus2};
```

---

#### Numerical Operators

> Verilog also provides a set of numerical operators as follows:

| Syntax | Operation |
| - | - | 
\+  | Addition
\-  | Subtraction (when placed between arguments)
\-  | 2’s complement negation (when placed in front of an argument)
\*  | Multiplication
/   | Division
%   | Modulus
**  | Raise to the power
<<< | Shift to the left, fill with zeros
\>>> | Shift to the right, fill with sign bit

> Verilog will allow the use of these operators on arguments of different sizes, types, and signs. The rules of the operations are as follows:

1. If two vectors are of different sizes, the smaller vector is expanded to the size of the larger vector.
    * If the smaller vector is unsigned, it is padded with zeros.
    * If the smaller vector is signed, it is padded with the sign bit.
2. If one of the arguments is real, then the arithmetic will take place using real numbers.
3. If one of the arguments is unsigned, then all arguments will be treated as unsigned.

---

#### Operator Precedence

> The following is the order of precedence of the Verilog operators:

| Operators | Precedence | Notes |
| - | - | - |
! ~ + -     | Highest | Bitwise/Unary
{} {{}}     | v | Concatenation/replication
()          | v | No operation, just parenthesis
**          | v | Power
\* / %      | v | Binary Multiply/Divide/Modulo
\+ -        | v | Binary Addition/Subtraction
<< >> <<< >>> | v | Shift Operators
< <= > >=   | v | Greater/Less than Comparisons
== !=       | v | Equality/Inequality Comparisons
& ~&        | v | AND/NAND Operators
^ ~^        | v | XOR/XNOR Operators
\| ~\|      | v | OR/NOR Operators
&&          | v | Boolean AND
\|\|        | v | Boolean OR
?:          | Lowest | Conditional Operator


## Concurrent

> It is important to remember that Verilog is a hardware description language, not a programming language. In a programming language, the lines of code are executed sequentially as they appear in the source file. In Verilog, the lines of code represent the behavior of real hardware. Thus, the assignments are executed concurrently unless specifically noted otherwise.

verilog这类HDL和C类的编程语言是有本质区别的，C类语言的代码是一行一行执行的，verilog描述的是硬件的行为，除非特殊指定，赋值语句都是并行执行的。


### Continuous Assignment

> Verilog uses the keyword assign to denote a continuous signal assignment. After this keyword, an assignment is made using the = symbol. The left-hand side (LHS) of the assignment is **the target signal and must be a net type**. The right-hand side (RHS) contains the input arguments and can contain nets, regs, constants, and operators. **A continuous assignment models combinational logic**. Any change to the RHS of the expression will result in an update to the LHS target net. The net being assigned to must be declared prior to the first continuous assignment. Multiple continuous assignments can be made to the same net. When this happens, **the assignment containing signals with the highest drive strength will take priority**.

verilog使用`assign`表示持续赋值，左边必须是net类型，因为持续赋值是用于表示组合逻辑的，右边可以是net/regs/常量。如果一个net被多次赋值，则取强度最高的。


```verilog
assign X = A;   // X is updated anytime A changes, where A is a signal
assign Y = B;
assign Z = C;
```

> Each individual assignment will be executed concurrently and synthesized as separate logic circuits. When simulated, these three lines of Verilog will make three separate signal assignments at the exact same time. This is different from a programming language that will first assign A to X, then B to Y, and finally C to Z. In Verilog, this functionality is identical to three separate wires. This description will be directly synthesized into three separate wires. 

三个赋值语句是并行的，和C类编程语言有本质区别。

---

```verilog
assign A = B;
assign B = C;
```

> In a Verilog simulation, the signal assignments of C to B and B to A will take place at the same time. This means during synthesis, the signal B will be eliminated from the design since this functionality describes two wires in a series. Automated synthesis tools will eliminate this unnecessary signal name. This is not the same functionality that would result if this example was implemented as a sequentially executed computer program. A computer program would execute the assignment of B to A first, then assign the value of C to B second. In this way, B represents a storage element that is passed to A before it is updated with C.

上例更加清楚的说明了verilog和C类语言的不同，在verilog中A和B都表示wire，等价于`A=C`, B可以忽略，但是在C类语言中，B表示一个内存位置，是不能省略的。

---

使用verilog实现`F = A'B'C' + A'BC' + ABC'`

```verilog
module SystemX(output wire F,
                input wire A, B, C);
    wire An, Bn, Cn;    // internal nets
    wire m0, m2, m6;

    assign An = ~A;
    assign Bn = ~B;
    assign Cn = ~C;

    assign m0 = An & Bn & Cn;
    assign m2 = An & B  & Cn;
    assign m6 = A  & B  & Cn;

    assign F = m0 | m2 | m6;
endmodule
```

另一种实现方式是直接根据真值表，然后利用条件赋值实现。

A B C | F  
\----------  
0 0 0 | 1  
0 0 1 | 0  
0 1 0 | 1  
0 1 1 | 0  
\----------  
1 0 0 | 0  
1 0 1 | 0  
1 1 0 | 1  
1 1 1 | 0  

```verilog
module SystemX(output wire F,
                input wire A, B, C);

    assign F = ((A == 1'b0) && (B == 1'b0) && (C == 1'b0)) ? 1'b1 :
               ((A == 1'b0) && (B == 1'b1) && (C == 1'b0)) ? 1'b1 :
               ((A == 1'b1) && (B == 1'b1) && (C == 1'b0)) ? 1'b1 :
               1'b0; 

endmodule
```

等价于下面这种写法

```verilog
module SystemX(output wire F,
                input wire A, B, C);

    assign F = (!A && !B && !C) ? 1'b1 :
               (!A &&  B && !C) ? 1'b1 :
               ( A &&  B && !C) ? 1'b1 :
               1'b0; 

endmodule
```

### Continuous Assignment with Delay

> Verilog provides the ability to model gate delays when using a continuous assignment. The # is used to indicate a delayed assignment. For combinational logic circuits, the delay can be specified for all transitions, for rising and falling transitions separately, and for rising, falling, and transitions to the value off separately. A transition to off refers to a transition to Z. If only one delay parameter is specified, it is used to model all delays. If two delay parameters are specified, the first parameter is used for the rise time delay while the second is used to model the fall time delay. If three parameters are specified, the third parameter is used to model the transition to off. Parenthesis is optional but recommended when using multiple delay parameters. When using delay, it is typical to include the `timescale directive to provide the units of the delay being specified.

```verilog
assign #(<del_all>) <target_net> = <RHS_nets, operators, etc. . .>;
assign #(<del_rise, del_fall>) <target_net> = <RHS_nets, operators, etc. . .>;
assign #(<del_rise, del_fall, del_off>) <target_net> = <RHS_nets, operators, etc. . .>;
```

赋值语句支持指定延迟参数，可以分别指定上升沿、下降沿、Z值的延迟。同时需要使用`timescale指令指定延迟的时间单位。

![v-assign-delay](/assets/images/2024-02-11/v-assign-delay.png)

> Verilog also provides a mechanism to model a range of delays that are selected by a switch set in the CAD compiler. There are three delay categories that can be specified: minimum, typical, and maximum. The delays are separated by a “:”. The following is the syntax of how to use the delay range capability.

`assign #(<min>:<typ>:<max>) <target_net> = <RHS_nets, operators, etc. . .>;`

verilog还支持指定延迟的范围，指定最小/典型/最大延迟参数。

```verilog
assign #(1:2:3) F = A; // Specifying a range of delays for all transitions.
assign #(1:1:2, 2:2:3) F = A; // Specifying a range of delays for rising/falling.
assign #(1:1:2, 2:2:3, 4:4:5) F = A; // Specifying a range of delays for each transition.
```


## Structural Design and Hierarchy

> Structural design in Verilog refers to including lower-level subsystems within a higher-level module in order to produce the desired functionality. This is called hierarchy and is a good design practice because it enables design partitioning. A purely structural design will not contain any behavioral constructs in the module such as signal assignments, but instead just contain the instantiation and interconnections of other subsystems. A subsystem in Verilog is simply another module that is called by a higher-level module. Each lower-level module that is called is executed concurrently by the calling module.

verilog支持结构化编程，一个模块可以在内部实例化其他模块，好比是C类语言在函数中调用其他函数以实现特定功能。

### 模块实例化

> The term instantiation refers to the use or inclusion of a lower-level module within a system. In Verilog, the syntax for instantiating a lower-level module is as follows.

`module_name <instance_identifier> (port mapping. . .);`

> The first portion of the instantiation is the module name that is being called. This must match the lower-level module name exactly, including case. The second portion of the instantiation is an optional instance identifier. Instance identifier is useful when instantiating multiple instances of the same lowerlevel module. The final portion of the instantiation is the port mapping. There are two techniques to connect signals to the ports of the lower-level module, explicit and positional.

端口映射有两种方式：显示的映射需要指定参数名，位置映射按参数位置一一对应，类型python中函数的命名参数和位置参数。

* **Explicit Port Mapping** In explicit port mapping, the names of the ports of the lower-level subsystem are provided along with the signals they are being connected to. The lower-level port name is preceded with a period (.) while the signal it is being connected is enclosed within parenthesis. The port connections can be listed in any order since the details of the connection (i.e., port name to signal name) are explicit. Each connection is separated by a comma. The syntax for explicit port mapping is as follows:

`module_name <instance identifier> (.port_name1(signal1), .port_name2(signal2), etc.);`

* **Positional Port Mapping** In positional port mapping, the names of the ports of the lower-level modules are not explicitly listed. Instead, the signals to be connected to the lower-level system are listed in the same order in which the ports were defined in the sub-system. Each signal name is separated by a comma. This approach requires less text to describe the connection but can also lead to misconnections due to inadvertent mistakes in the signal order. The syntax for positional port mapping is as follows:

`module_name <instance_identifier> (signal1, signal2, etc.);`


```verilog

module Sub(output wire W, 
             input wire A);
    // behavior here ...
endmodule

module SystemExplicit(output wire Z,
                       input wire X, Y);
    wire B;
    Sub U0(.W(B), .A(X));   // 命名参数
    assign Z = B && Y;
endmodule

module SystemPositional(output wire Z,
                         input wire X, Y);
    wire B;
    Sub U0(B, X);   // 位置参数
    assign Z = B && Y;
endmodule

```

### Gate Level Primitives

> Verilog provides the ability to model basic logic functionality through the use of primitives. A primitive is a logic operation that is simple enough that it doesn’t require explicit modeling. An example of this behavior can be a basic logic gate or even a truth table. Verilog provides a set of gate-level primitives to model simple logic operations. These gate level primitives are **not(), and(), nan (), or(), nor(), xor(), and xnor()**. Each of these primitives is instantiated as lower-level subsystems with positional port mapping. The port order for each primitive has the output listed first followed by the input(s). The output and each of the inputs are scalars. Gate-level primitives do not need to be explicitly created as they are provided as part of the Verilog standard. One of the benefits of using gate-level primitives is that the number of inputs is easily scaled as each primitive can accommodate an increasing number of inputs automatically. Furthermore, modeling using this approach essentially provides a gate-level netlist, so it represents a very low-level, detailed gate-level implementation that is ready for technology mapping.

verilog提供了gate级的基础操作对应的内置模块，实现`F = A'B'C' + A'BC' + ABC'`的另一种方式如下：

```verilog
module SystemX(output wire F,
                input wire A, B, C);
    wire An, Bn, Cn;    // internal nets
    wire m0, m2, m6;

    not U0(An, A);
    not U1(Bn, B);
    not U2(Cn, C);

    and U3(m0, An, Bn, Cn);
    and U4(m2, An, B , Cn);
    and U5(m6, A , B , Cn);

    or U6(F, m0, m2, m6);
endmodule
```

### User-Defined Primitives

> A user-defined primitive (UDP) is a system that describes the behavior of a low-level component using a logic table. This is very useful for creating combinational logic functionality that will be used numerous times. UDPs are also useful for large truth tables where it is more convenient to list the functionality in table form. UDPs are lower-level subsystems that are intended to be instantiated in higher-level modules just like gate-level primitives, with the exception that the UDP needs to be created in its own file. The syntax for a UDP is as follows:

```verilog
primitive primitive_name(output output_name,
                          input input_name1, input_name2, ...);
    table
        in1_val in2_val ... : out_val;
        in1_val in2_val ... : out_val;
        ...
    endtable
endprimitive
```

> A UDP must list its output(s) first in the port definition. It also does not require types to be defined for the ports. For combinational logic UDPs, all ports are assumed to be of type wire.

UDP类似于内置的基础模块，直接利用真值表实现逻辑功能，例如模拟内置的and如下：

```verilog
primitive and(output Y,
               input A, B);
    table
     // A B : Y
        0 0 : 0;
        0 1 : 0;
        1 0 : 0;
        1 1 : 1;
    endtable
endprimitive
```

## 示例

### One-Hot Decoder

> A one-hot decoder is a circuit that has n inputs and 2n outputs. Each output will assert for one and only one input code. Since there are 2n outputs, there will always be one and only one output asserted at any given time. 

解码器可以用于解码指令等，有两种实现思路：逻辑操作符或条件赋值

![v-decoder-logic](/assets/images/2024-02-11/v-decoder-logic.png)

![v-decoder-condition](/assets/images/2024-02-11/v-decoder-condition.png)

### One-Hot Binary Encoder

> A one-hot binary encoder has n outputs and 2n inputs. The output will be an n-bit, binary code that corresponds to an assertion on one and only one of the inputs.

![v-encoder](/assets/images/2024-02-11/v-encoder.png)

### Multiplexers

> A multiplexer is a circuit that passes one of its multiple inputs to a single output based on a select input. This can be thought of as a digital switch. The multiplexer has n select lines, 2n inputs, and one output.

![v-mux](/assets/images/2024-02-11/v-mux.png)

### Demultiplexers

> A demultiplexer works in a complementary fashion to a multiplexer. A demultiplexer has one input that is routed to one of its multiple outputs. The output that is active is dictated by a select input. A demux has n select lines that choose to route the input to one of its 2n outputs. When an output is not selected, it outputs a logic 0.

![v-demux](/assets/images/2024-02-11/v-demux.png)


