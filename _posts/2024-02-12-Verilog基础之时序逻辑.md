---
layout: post
title:  "Verilog基础之时序逻辑"
date:   2024-02-12 23:22:07 +0000
categories: jekyll
tags: hardware
---

# Verilog基础之时序逻辑

## Procedural Assignment

> Verilog uses procedural assignment to model signal assignments that are based on an event. An event is most commonly a transition of a signal. This provides the ability to model sequential logic circuits such as D-flip-flops and finite state machines by triggering assignments off of a clock edge. Procedural assignments can only drive variable data types (i.e., reg, integer, real, and time), thus they are ideal for modeling storage devices. Procedural signal assignments can be evaluated in the order they are listed, so they are able to model sequential assignments.

> A procedural assignment can also be used to model combinational logic circuits by making signal assignments when any of the inputs to the model change. Despite the left-hand side of the assignment not being able to be of type wire in the procedural assignment, modern synthesizers will recognize properly designed combinational logic models and produce the correct circuit implementation. Procedural assignment also supports standard programming constructs such as if-else decisions, case statements, and loops. This makes procedural assignment a powerful modeling approach in Verilog and is the most common technique for designing digital systems and creating test benches.

verilog使用过程赋值实现基于事件触发的赋值，这种模型非常适合基于CLK触发的时序电路。过程赋值也能时序组合逻辑，只要右操作数都是监听的事件列表中。

### Procedural Blocks

> All procedural signal assignments must be enclosed within a procedural block. Verilog has two types of procedural blocks, initial and always.

* **Initial Blocks** An initial block will execute all of the statements embedded within it one time at the beginning of the simulation. An initial block is not used to model synthesizable behavior. It is instead used within test benches to either set the initial values of repetitive signals or to model the behavior of a signal that only has a single set of transitions. The following is the syntax for an initial block:

```verilog
initial
    begin   // an optional “: name” can be added after the begin keyword
        signal_assignment_1
        signal_assignment_2
        ...
    end
```

初始块只用于仿真，用于设置变量初始值。下面是一个关于reset的示例，利用延时重置reset

```verilog
initial
    begin
            Reset_TB = 1’b0;
        #15 Reset_TB = 1’b1;
    end
```

* **Always Blocks** An always block will execute forever or for the duration of the simulation. An always block can be used to model synthesizable circuits in addition to non-synthesizable behavior in test benches. The following is the syntax for an always block:

```verilog
always
    begin
        signal_assignment_1
        signal_assignment_2
        ...
    end
```

always块会持续执行，下面是一个关于clock的示例，利用延时定时切换clock

```verilog
initial
    begin
        Clock_TB = 1’b0;
    end

always
    begin
        #10 Clock_TB = ~Clock_TB;
    end
```

---

> A sensitivity list is used in conjunction with a procedural block to trigger when the assignments within the block are executed. The symbol @ is used to indicate a sensitivity list. Signals can then be listed within parenthesis after the @ symbol that will trigger the procedural block. The following is the base syntax for a sensitivity list:

```verilog
always @ (signal1, signal2)
    begin
        signal_assignment_1
        signal_assignment_2
        ...
    end
```

> In this syntax, any transition on any of the signals listed within the parenthesis will cause the always block to trigger and all of its assignments to take place one time. After the always block ends, it will await the next signal transition in the sensitivity list to trigger again.

always块支持指定敏感列表，即监听的信号列表，列表中任意信号发生变化都会触发always块执行一次。

> Verilog also supports keywords to limit the triggering of the block to only rising edge or falling edge transitions. The keywords are **posedge** and **negedge**. The following is the base syntax for an edgesensitive block. In this syntax, only rising edge transitions on signal1 or falling edge transitions on signal2 will cause the block to trigger.

```verilog
always @ (posedge signal1, negedge signal2)
    begin
        signal_assignment_1
        signal_assignment_2
        ...
    end
```

verilog支持更精细化的指定敏感列表中监听的事件变化的时机：上升沿或下降沿

> Sensitivity lists can also contain Boolean operators to more explicitly describe behavior. The following syntax is identical to the syntax above.

```verilog
always @ (posedge signal1 or negedge signal2)
    begin
        signal_assignment_1
        signal_assignment_2
        ...
    end
```

verilog允许敏感列表中使用布尔操作，默认的敏感列表中的信号是或的关系

> In Verilog-2001, the syntax to support sensitivity lists that will trigger based on any signal listed on the right-hand side of any assignment within the block was added. This syntax is `@*`. The following example shows how to use this modeling approach to model a 3-input AND gate.

```verilog
always @*
    begin
        F = A & B & C;
    end
```

verilog提供快捷方式以监听所有右操作数


### Procedural Statements

> There are two kinds of signal assignments that can be used within a procedural block, blocking and nonblocking.

* **Blocking Assignments** A blocking assignment is denoted with the = symbol, and the evaluation and assignment of each statement take place immediately. Each assignment within the block is executed in parallel. When this behavior is coupled with a sensitivity list that contains all of the inputs to the system, this approach can model synthesizable combinational logic circuits. This approach provides the same functionality as continuous assignments outside of a procedural block. The reason that designers use blocking assignments instead of continuous assignments is that more advanced programming constructs are supported within Verilog procedural blocks. 

* **Nonblocking Assignments** A nonblocking assignment is denoted with the <= symbol. When using nonblocking assignments, the assignment to the target signal is deferred until the end of the procedural block. This allows the assignments to be executed in the order they are listed in the block without cascading interim assignments through the list. When this behavior is coupled with triggering the block off of a clock signal, this approach can model synthesizable sequential logic circuits.

阻塞赋值的语句立即生效且并发执行，把右操作数都加入敏感列表就可以用于表示可综合的组合电路。非阻塞赋值的语句顺序执行且延迟生效，把clock加入敏感列表就可以用于表示可综合的时序电路。

>  One source of confusion comes from the fact that blocking and nonblocking assignments can produce the same results when they either contain a single assignment or a list of assignments that don’t have any signal interdependencies. A signal interdependency refers to when a signal that is the target of an assignment (i.e., on the LHS of an assignment) is used as an argument (i.e., on the RHS of an assignment) in subsequent statements.

如果左右操作数之间没有依赖关系，阻塞赋值和非阻塞赋值产生的电路是一样的。下面两个module都表示同一个电路：

![v-without-depend](/assets/images/2024-02-12/v-without-depend.png)


```verilog
module BlockingEx(output reg Y, Z,
                   input wire A, B, C);
    always @ (A, B, C)
        begin
            Y = A & B;
            Z = B | C;
        end
endmodule

module NonBlockingEx(output reg Y, Z,
                      input wire A, B, C);
    always @ (A, B, C)
        begin
            Y <= A & B;
            Z <= B | C;
        end
endmodule
```

---

如果左右操作数之间有依赖，阻塞赋值和非阻塞赋值会产生不一样的效果。

```verilog
module BlockingEx(output reg S,
                   input wire A, B, C);
    reg n1;

    always @ (A, B, C)
        begin
            n1 = A ^ B;
            S = n1 ^ C;
        end
endmodule

module NonBlockingEx(output reg S,
                      input wire A, B, C);
    reg n1;

    always @ (A, B, C)
        begin
            n1 <= A ^ B;
            S <= n1 ^ C;
        end
endmodule
```

上述两个模块，n1的依赖都在敏感列表中，所以n1是一样的。阻塞赋值时S依赖的是最新的n1的值，表示组合逻辑，非阻塞赋值时S依赖的是n1的上一个值，看起来是表示时序逻辑，但是clock又不在敏感列表里，所以有点混乱。

---

```verilog
module BlockingEx(output reg S,
                   input wire A, Clock);
    reg B;

    always @ (posedge Clock)
        begin
            B = A;
            S = B;
        end
endmodule

module NonBlockingEx(output reg S,
                      input wire A, Clock);
    reg B;

    always @ (posedge Clock)
        begin
            B <= A;
            S <= B;
        end
endmodule
```

上述两个模块，阻塞赋值时等价于`S = B = A`，A的变化间隔一个周期传播到S，非阻塞赋值时A的变化间隔两个周期传播到S。

---

> While the behavior of these procedural assignments can be confusing, there are two design guidelines that can make creating accurate, synthesizable models straightforward. They are:

1. When modeling combinational logic, use blocking assignments and list every input in the sensitivity list.
2. When modeling sequential logic, use nonblocking assignments and only list the clock and reset lines (if applicable) in the sensitivity list

为了避免迷惑，需要遵循两个原则：描述组合逻辑时使用组合赋值且右操作数都加入敏感列表；描述时序逻辑时使用非阻塞赋值且只把clock加入敏感列表。

### Statement Groups

> A statement group refers to how the statements in a block are processed. Verilog supports two types of statement groups: begin/end and fork/join. When using begin/end, all statements enclosed within the group will be evaluated in the order they are listed. When using a fork join, all statements enclosed within the group will be evaluated in parallel. When there is only one statement within a procedural block, a statement group is not needed. For multiple statements in a procedural block, a statement group is required. Statement groups can contain an optional name that is appended after the first keyword, preceded by a “:”. 

```verilog
module GroupEx1();
    reg[7:0] S_TB;

    initial
        begin: Ex1
                S_TB = 8'h00;
            #10 S_TB = 8'h55;
            #15 S_TB = 8'hAA;
        end
endmodule

module GroupEx2();
    reg[7:0] S_TB;

    initial
        fork: Ex1
                S_TB = 8'h00;
            #10 S_TB = 8'h55;
            #15 S_TB = 8'hAA;
        join
endmodule
```

![v-fork-join](/assets/images/2024-02-12/v-fork-join.png)

verilog支持两种语句块：bigin/end内的语句顺序执行；fork/join内的语句并行执行

### Local Variables

> Local variables can be declared within a procedural block. The statement group must be named, and the variables will not be visible outside of the block. Variables can only be of variable type.

```verilog
initial
    begin: stim_block   // it is required to name the block when declaring local variables
        integer i;      // local variables can only be of variable type
        i=2;
    end
```


## Conditional Programming Constructs

> One of the more powerful features that procedural blocks provide in Verilog is the ability to use conditional programming constructs such as if-else decisions, case statements, and loops. These constructs are only available within a procedural block and can be used to model both combinational and sequential logic.

### if-else Statements

```verilog
if (<boolean_condition>)
    true_statement
else
    false_statement

if (<boolean_condition>)
    begin
        true_statement_1
        true_statement_2
    end
else
    begin
        false_statement_1
        false_statement_2
    end

if (<boolean_condition_1>)
    true_statement_1
else if (<boolean_condition_2>)
    true_statement_2
else
    false_statement
```

verilog的if语句完全抄袭了C语言，示例如下：

A B C | F  
\----------  
0 0 0 | 1  
0 0 1 | 0  
0 1 0 | 1  
0 1 1 | 0  
\----------  
1 0 0 | 0  
1 0 1 | 0  
1 1 0 | 1  
1 1 1 | 0  

```verilog
module SystemX(output reg F,
                input wire A, B, C);

    always @ (A, B, C)
        begin
            if ((A == 1'b0) && (B == 1'b0) && (C == 1'b0))
                F = 1'b1;
            else if ((A == 1'b0) && (B == 1'b1) && (C == 1'b0))
                F = 1'b1;
            else if ((A == 1'b1) && (B == 1'b1) && (C == 1'b0))
                F = 1'b1;
            else
                F = 1'b0;
        end

endmodule
```

### case Statements

```verilog
case (<input_name>)
    input_val_1 : statement_1
    input_val_2 : statement_2
    ...
    input_val_n : statement_n
    default : default_statement
endcase
```

case语句借鉴了C语言的switch语句，示例如下：

```verilog
module SystemX(output reg F,
                input wire A, B, C);

    always @ (A, B, C)
        begin
            case ({A, B, C})
                3'b000 : F = 1'b1;
                3'b010 : F = 1'b1;
                3'b110 : F = 1'b1;
                default: F = 1'b0;
            endcase
        end

endmodule
```

### while Loops

```verilog
while (<boolean_condition>)
    begin
        statement_1
        statement_2
        :
        statement_n
    end
```

while语句也借鉴了C语言

```verilog
initial
    begin
        CLK = 0;
        while (EN == 1)
            #10 CLK = ~CLK;
    end
```

### for Loops

```verilog
for (<initial_assignment>; <Boolean_condition>; <step_assignment>)
    begin
        statement_1
        statement_2
        :
        statement_n
    end
```

```verilog
initial
    begin
        for (i=0; i<15; i=i+1)
            #10 Count = i;
    end
```

### disable

```verilog
initial
    begin
    CLK = 0;
        forever
        begin: loop_ex
            if (EN == 1)
                #10 CLK = ~CLK;
            else
                // The group name to be disabled comes after the keyword
                disable loop_ex; 
        end
    end
```

disable相当于C语言中的break


## System Tasks

> A system task in Verilog is one that is used to insert additional functionality into a model that is not associated with real circuitry. There are three main groups of system tasks in Verilog: (1) text output; (2) file input/output; and (3) simulation control. All system tasks begin with a $ and are only used during simulation. These tasks are ignored by synthesizers, so they can be included in real circuit models. All system tasks must reside within procedural blocks.

verilog支持一些特殊操作比如打印变量信息，综合时忽略这些特殊操作。

### Text Output

> Text output system tasks are used to print strings and variable values to the console or transcript of a simulation tool. The syntax follows ANSI C, where double quotes (“”) are used to denote the text string to be printed.

| Task | Description |  
| - | - |  
$display() | Print text string when statement is encountered and append a newline.
$displayb() | Same as $display, but default format of any arguments is binary.
$displayo() | Same as $display, but default format of any arguments is octal.
$displayh() | Same as $display, but default format of any arguments is hexadecimal.
$write() | Same as $display, but the string is printed without a newline.
$writeb() | Same as $write, but default format of any arguments is binary.
$writeo() | Same as $write, but default format of any arguments is octal.
$writeh() | Same as $write, but default format of any arguments is hexadecimal.
$strobe() | Same as $display, but printing occurs after all simulation events are executed.
$strobeb() | Same as $strobe, but default format of any arguments is binary.
$strobeo() | Same as $strobe, but default format of any arguments is octal.
$strobeh() | Same as $strobe, but default format of any arguments is hexadecimal.
$monitor() | Same as $display, but printing occurs when the value of an argument changes.
$monitorb() | Same as $monitor, but default format of any arguments is binary.
$monitoro() | Same as $monitor, but default format of any arguments is octal.
$monitorh() | Same as $monitor, but default format of any arguments is hexadecimal.
$monitoron | Begin tracking argument changes in subsequent $monitor tasks.
$monitoroff | Stop tracking argument changes in subsequent $monitor tasks.

`$display()`借鉴了C语言的`printf`，支持的格式如下：

| Code | Format |   
| - | - |  
%b | Binary values
%o | Octal values
%d | Decimal values
%h | Hexadecimal values
%f | Real values using decimal form
%e | Real values using exponential form
%t | Time values
%s | Character strings
%m | **Hierarchical name of scope** (no argument required when printing)
%l | Configuration library binding (no argument required when printing)

```verilog
// integer A = 3
$display("Hello World");    // Will print: Hello World
$display("A = %b", A);      // This will print: A = 00000000000000000000000000000011
$display("A = %o", A);      // This will print: A = 00000000003
$display("A = %d", A);      // This will print: A = 3
$display("A = %h", A);      // This will print: A = 00000003
$display("A = %4.0b", A);   // This will print: A = 0011
$write("A is ", A, "\n");   // This will print: A is 3
$writeb("A is ", A, "\n");  // This will print: A is 00000000000000000000000000000011
$writeo("A is ", A, "\n");  // Will print: A is 00000000003
$writeh("A is ", A, "\n");  // Will print: A is 00000003
```

### File Input/Output

> File I/O system tasks allow a Verilog module to create and/or access data files in the same way files are handled in ANSI C. This is useful when the results of a simulation are large and need to be stored in a file as opposed to being viewed in a waveform or transcript window. This is also useful when complex stimulus vectors are to be read from an external file and driven into a device under test (DUT). Verilog supports the following file I/O system task functions:

| Task | Description |  
| - | - | 
$fopen() | Opens a file and returns a unique file descriptor.
$fclose() | Closes the file associated with the descriptor.
$fdisplay() | Same as $display but statements are directed to the file descriptor.
$fwrite() | Same as $write but statements are directed to the file descriptor.
$fstrobe() | Same as $strobe but statements are directed to the file descriptor.
$fmonitor() | Same as $monitor but statements are directed to the file descriptor.
$readmemb() | Read binary data from a file and insert into a previously defined memory array.
$readmemh() | Read hexadecimal data from a file and insert into a previously defined memory array.

verilog支持像C语言一样操作文件，用于仿真

```verilog
reg[2:0] memory[7:0];

initial
    begin: Read_Block
        $readmemb("Data_in.txt", memory);
    end

initial
    begin: Print_Block
        $display("printing memory %b", memory[0]); // This will print “000”
        $display("printing memory %b", memory[1]); // This will print “001”
        $display("printing memory %b", memory[2]); // This will print “010”
        $display("printing memory %b", memory[3]); // This will print “011”
        $display("printing memory %b", memory[4]); // This will print “100”
        $display("printing memory %b", memory[5]); // This will print “101”
        $display("printing memory %b", memory[6]); // This will print “110”
        $display("printing memory %b", memory[7]); // This will print “111”
    end
```

### Simulation Control and Monitoring

> Verilog also provides a set of simulation control and monitoring tasks. The following are the most commonly used tasks in this group:

| Task | Description |  
| - | - | 
$finish() | Finishes simulation and exits.
$stop() | Halts the simulation and enters an interactive debug mode.
$timeformat() | Controls the format used by the \%t code in print statements. The arguments are: (<unit\>, <precision\>, <suffix\>, <min_field_width\>)

---

## 示例

### D-Flip-Flop

![v-dff](/assets/images/2024-02-12/v-dff.png)

```verilog
module dflipflop(output reg Q, Qn,
                  input wire Clock, Reset, Preset, D, EN);
    always @ (posedge Clock or negedge Reset or negedge Preset)
        if (!Reset)
            begin
                Q  <= 1'b0;
                Qn <= 1'b1;
            end
        else if (!Preset)
            begin
                Q  <= 1'b1;
                Qn <= 1'b0;
            end
        else
            if (EN)
                begin
                    Q  <=  D;
                    Qn <= ~D;
                end

endmodule
```


### FSM

TODO